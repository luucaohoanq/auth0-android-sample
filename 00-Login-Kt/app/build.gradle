plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.auth0.androidsample"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
//        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "@string/com_auth0_scheme"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Read from local.properties
        def properties = new Properties()
        def localPropsFile = rootProject.file("local.properties")
        def errorMessage = "local.properties file not found"
        if (localPropsFile.exists()) {
            properties.load(localPropsFile.newInputStream())
        } else {
            throw new RuntimeException("local.properties file not found")
        }

        def auth0Domain = properties.getProperty("auth0.domain")
        def auth0ClientId = properties.getProperty("auth0.client.id")
        if (auth0Domain == null || auth0ClientId == null) {
            throw new RuntimeException(errorMessage)
        }

        buildConfigField "String", "AUTH0_DOMAIN", "\"${auth0Domain}\""
        buildConfigField "String", "AUTH0_CLIENT_ID", "\"${auth0ClientId}\""

        manifestPlaceholders = [auth0Domain: auth0Domain, auth0Scheme: "@string/com_auth0_scheme"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.auth0.android:auth0:2.9.2'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
